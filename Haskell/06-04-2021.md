# Семинар 06.04.2021

## Полугруппы и моноиды

Полугруппа (Semigroup) -- множество элементов и заданная на нём двухместная ассоциативная операция. Пример: непустые строки с операцией конкатенации, натуральные числа без нуля с операцией сложения.

Моноид -- полугруппа с нейтральным элементом для заданной операции. Пример: строки с операцией конкатенации (пустая строка как нейтральный элемент), целые числа с операцией сложения и нулём, изображения с операцией наложения друг на друга.

В терминах Haskell: множество элементов -- тип `a`, операция -- функция `(<>) :: a -> a -> a`, нейтральный элемент -- константа `mempty` типа `a`. Полугруппу и моноид можно представить как классы типов:

```haskell
class Semigroup a where
  (<>) :: a -> a -> a

class Semigroup a => Monoid a where
  mempty :: a -- Нейтральный элемент
  mappend :: a -> a -> a
  mappend = (<>) -- Реализация по умолчанию
```

В более ранних версиях пакета `base` (~ набор стандартных модулей) был только класс `Monoid`:
```haskell
class Monoid a where
  mempty :: a
  mappend :: a -> a -> a
```

Законы моноида:
```
(x <> y) <> z == x <> (y <> z)
mempty <> x == x
x <> mempty == x
```

Реализуем `instance Monoid` для целых чисел для операций сложения и умножения. Это две разные реализации, поэтому мы не можем просто написать `instance Monoid Int where ...`. Используем `newtype`, чтобы сделать две обёртки над типом `Int`:

```haskell
newtype Sum = Sum Int deriving (Show)

instance Monoid Sum where
  mempty = Sum 0
  mappend (Sum x) (Sum y) = Sum (x + y)

newtype Prod = Prod Int deriving (Show)

instance Monoid Prod where
  mempty = Prod 1
  mappend (Prod x) (Prod y) = Prod (x * y)
```

Аналогично определим обёртки над типом `Bool` для операций логического И и ИЛИ:

```haskell
newtype All = All Bool deriving (Show)

instance Monoid All where
  mempty = All True
  mappend (All x) (All y) = All (x && y)

newtype Any = Any Bool deriving (Show)

instance Monoid Any where
  mempty = Any False
  mappend (Any x) (Any y) = Any (x || y)
```

Списки сами по себе являются моноидами:

```haskell
instance Monoid [a] where
  mempty = []
  mappend = (++)
```

Рассмотрим более сложный случай: упорядоченный список. Аналогично тому, как поступали с числами, создадим новый тип для таких списков:

```haskell
newtype OrderedList a = OrderedList [a] deriving (Show)

-- Здесь ещё появляется ограничение Ord a, т.к. элементы списка нужно уметь сравнивать
instance Ord a => Monoid (OrderedList a) where
  mempty = OrderedList []
  mappend (OrderedList l1) (OrderedList l2) = OrderedList $ mergeList l1 l2
    where
      mergeList l1 [] = l1
      mergeList [] l2 = l2
      mergeList (x:xs) (y:ys) =
        if x < y then
          x : (mergeList xs (y:ys))
        else
          y : (mergeList (x:xs) ys)
```

Функции -- довольно неожиданный пример моноида. Есть функция `id :: a -> a`, которая просто возвращает свой аргумент. Рассмотрим множество всех функций такого типа `a -> a`. К любым двум из них применима операция композиции, в результате которой также получается функция типа `a -> a`. Опять используем `newtype` и реализуем `instance Monoid` для таких функций:

```haskell
newtype Endo a = Endo (a -> a)

instance Monoid (Endo a) where
  mempty = Endo id
  mappend (Endo f) (Endo g) = Endo (f . g)
```

Пример работы:

```
>>> f = Endo ("Hello, " ++)
>>> :t f
f :: Endo [Char]
>>> g = Endo (++ "!")
>>> :t g
g :: Endo [Char]
>>> Endo h = f <> g
>>> h "John"
"Hello, John!"
```

## Моноидная свёртка

Свёртка используется для преобразования сложной структуры данных в одно атомарное значение. Для свёртки нужна функция, определяющая, как комбинируются значения, и начальное значение/аккумулятор, которое используется, когда структура "заканчивается". Свёртка для списков имеет такой тип:
```haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
```

Сворачивать можно не только списки, но и, например, деревья, векторы и т.п. Сделаем тип более общим, заменив конструктор типа `[]` на переменную `t` (она обозначает тип вида `* -> *`):
```haskell
foldr :: (a -> b -> b) -> b -> t a -> b
```

Представим, что в структуре хранятся значения типа-моноида, и в результате мы тоже хотим получить значение этого типа, т.е. `a = b = m`:
```
foldr :: (m -> m -> m) -> m -> t m -> m
```

Теперь функция для комбинирования значений имеет тип `m -> m -> m`, такой же как `mappend`, а в качестве начального значения можно использовать `mempty`. Поэтому их можно явно не указывать, а для свёртки структур с моноидами использовать функцию типа `Monoid m => t m -> m`.

Класс типов `Foldable` описывает структуры, которые можно свернуть. У него много методов, но обязательными для реализации являются `fold` или `foldMap`:

```haskell
class Foldable t where
  fold :: Monoid m => t m -> m
  foldMap :: Monoid m => (a -> m) -> t a -> m
```

Функция `foldMap` принимает на вход функцию, преобразующую тип в структуре в моноид, саму структуру и производит свёртку с использованием `mappend` и `mempty`, предварительно преобразуя элементы в структуре.

Рассмотрим для примера тип дерева:
```haskell
data Tree a = Empty | Node a (Tree a) (Tree a) deriving (Show)

instance Foldable Tree where
  foldMap f Empty = mempty
  foldMap f (Node x l r) = f x <> foldMap f l <> foldMap f r
```

Эту реализацию можно получить автоматически, использовав расширение `DeriveFoldable`:
```haskell
{-# LANGUAGE DeriveFoldable #-}

data Tree a = Empty | Node a (Tree a) (Tree a) deriving (Show, Foldable)
```

В модуле `Data.Monoid` уже реализованы моноиды, которые мы рассматривали сегодня (`Sum`, `Product`, `Any`, `All` и многие другие). Их удобно использовать для свёрток. Например, чтобы проверить, все ли символы в дереве являются цифрами:
```
>>> testTree = Node 'a' (Node '1' Empty Empty) Empty
>>> foldMap (All . isDigit) testTree
All {getAll = False}
>>> foldMap (Any . isDigit) testTree
Any {getAny = True}
```

Или чтобы посчитать сумму/произведение чисел в дереве:
```
>>> testTree2 = Node 1 (Node 2 Empty Empty ) (Node 4 Empty Empty)
>>> foldMap Sum testTree2
Sum {getSum = 7}
>>> foldMap Product testTree2
Product {getProduct = 8}
```
